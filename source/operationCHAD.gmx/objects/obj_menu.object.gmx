<!--This Document is generated by GameMaker, if you edit it by hand then you do so at your own risk!-->
<object>
  <spriteName>&lt;undefined&gt;</spriteName>
  <solid>0</solid>
  <visible>-1</visible>
  <depth>0</depth>
  <persistent>0</persistent>
  <parentName>&lt;undefined&gt;</parentName>
  <maskName>&lt;undefined&gt;</maskName>
  <events>
    <event eventtype="0" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>background_color = global.DARKpurple;

// init settings
config_init();
// load settings if they exist or save them if they don't
config_load();
config_save();

// view/window/gui
view_xview[0] = 0;
view_yview[0] = 0;
view_wview[0] = 400;
view_hview[0] = 240;

display_reset(0,global.CFG[config.vsync]);
if (global.CFG[config.fullscreen])
    window_set_fullscreen(true);
else
    {
    var _scl = global.CFG[config.windowScale];
    window_set_size(view_wview[0]*_scl,view_hview[0]*_scl);
    window_set_position(display_get_width()/2-(view_wview[0]/2)*_scl,display_get_height()/2-(view_hview[0]/2)*_scl);
    }
surface_resize(application_surface,view_wview[0],view_hview[0]);
display_set_gui_size(view_wview[0],view_hview[0]);

// gamepad
gamepad_init();

// game globals
global.pause = false;
global.music = -1;
global.FODmin = 160;
global.FODmax = 256;
global.xnudge = 0;
global.ynudge = 0;
global.xshake = 0;
global.yshake = 0;

// create shader controller
if (!instance_exists(obj_shader))
    instance_create(0,0,obj_shader);

menu = 0;
select = 0;
selectx = 0;
selecty = 0;
rebind = false;

bg_alpha = 1;

CLOUDx = 0;
CLOUDspe = -3;
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="3" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>gamepad_update();

var pressed = false;
if (keyboard_check_pressed(ord("Z")))
or (keyboard_check_pressed(ord("X")))
or (gamepad_check_pressed_all(gpinput.face1))
or (gamepad_check_pressed_all(gpinput.face3))
    pressed = true;

switch(menu)
    {
    case 0: // title screen
        if (pressed)
            menu = 1;
        break;
    
    case 1: // main menu
        if (pressed)
            {
            if (select == 0)
                room_goto_next();
            else if (select == 2)
                {
                menu = 2;
                select = 0;
                }
            else if (select == 3)
                game_end();
            }
        
        if (keyboard_check_pressed(vk_up))
        or (gamepad_check_pressed_all(gpinput.padu))
            select = max(0,select-1);
        if (keyboard_check_pressed(vk_down))
        or (gamepad_check_pressed_all(gpinput.padd))
            select = min(3,select+1);
        break;
    
    case 2: // settings
        if (pressed)
            {
            if (select == 0)
                {
                menu = 3; // video
                select = 0;
                }
            else if (select == 1)
                {
                menu = 4; // audio
                select = 0;
                }
            else if (select == 2)
                {
                menu = 5; // keyboard controls
                selectx = 0;
                selecty = 0;
                }
            else if (select == 3)
                {
                menu = 6; // gamepad controls
                selectx = 0;
                selecty = 0;
                }
            else if (select == 4) // back
                {
                menu = 1;
                select = 0;
                config_save();
                }
            }
        
        if (keyboard_check_pressed(vk_up))
        or (gamepad_check_pressed_all(gpinput.padu))
            select = max(0,select-1);
        if (keyboard_check_pressed(vk_down))
        or (gamepad_check_pressed_all(gpinput.padd))
            select = min(4,select+1);
        break;
    
    case 3: // video settings
        if (pressed)
            {
            if (select == 0)
                {
                global.CFG[config.fullscreen] = !global.CFG[config.fullscreen];
                window_set_fullscreen(global.CFG[config.fullscreen]);
                if (!global.CFG[config.fullscreen])
                    {
                    var _scl = global.CFG[config.windowScale];
                    window_set_size(view_wview[0]*_scl,view_hview[0]*_scl);
                    window_set_position(display_get_width()/2-(view_wview[0]/2)*_scl,display_get_height()/2-(view_hview[0]/2)*_scl);
                    }
                }
            else if (select == 1)
                {
                global.CFG[config.windowScale]++;
                
                var dw = display_get_width();
                var dh = display_get_height();
                if (view_wview[0]*global.CFG[config.windowScale] &gt; dw)
                or (view_hview[0]*global.CFG[config.windowScale] &gt; dh)
                    global.CFG[config.windowScale] = 1;
                
                if (!global.CFG[config.fullscreen])
                    {
                    var _scl = global.CFG[config.windowScale];
                    window_set_size(view_wview[0]*_scl,view_hview[0]*_scl);
                    window_set_position(display_get_width()/2-(view_wview[0]/2)*_scl,display_get_height()/2-(view_hview[0]/2)*_scl);
                    }
                }
            else if (select == 2) // vsync
                {
                global.CFG[config.vsync] = !global.CFG[config.vsync];
                }
            else if (select == 3)
                {
                global.CFG[config.CRTwarp] += 0.25;
                if (global.CFG[config.CRTwarp] &gt; 1)
                    global.CFG[config.CRTwarp] = 0;
                }
            else if (select == 4)
                {
                global.CFG[config.CRTscan] += 0.25;
                if (global.CFG[config.CRTscan] &gt; 1)
                    global.CFG[config.CRTscan] = 0;
                }
            else if (select == 5)
                global.CFG[config.particles] = !global.CFG[config.particles];
            else if (select == 6)
                {
                display_reset(0,global.CFG[config.vsync]);
                
                if (global.CFG[config.fullscreen])
                    window_set_fullscreen(true);
                else
                    {
                    var _scl = global.CFG[config.windowScale];
                    window_set_size(view_wview[0]*_scl,view_hview[0]*_scl);
                    window_set_position(display_get_width()/2-(view_wview[0]/2)*_scl,display_get_height()/2-(view_hview[0]/2)*_scl);
                    }
                surface_resize(application_surface,view_wview[0],view_hview[0]);
                display_set_gui_size(view_wview[0],view_hview[0]);
                
                menu = 2;
                select = 0;
                }
            }
        
        if (keyboard_check_pressed(vk_up))
        or (gamepad_check_pressed_all(gpinput.padu))
            select = max(0,select-1);
        if (keyboard_check_pressed(vk_down))
        or (gamepad_check_pressed_all(gpinput.padd))
            select = min(7,select+1);
        break;
    
    case 4: // audio settings
        if (pressed)
            {
            if (select == 0)
                {
                global.CFG[config.BGMgain] += 0.25;
                if (global.CFG[config.BGMgain] &gt; 1)
                    global.CFG[config.BGMgain] = 0;
                }
            else if (select == 1)
                {
                global.CFG[config.SFXgain] += 0.25;
                if (global.CFG[config.SFXgain] &gt; 1)
                    global.CFG[config.SFXgain] = 0;
                }
            else if (select == 2)
                {
                menu = 2;
                select = 0;
                }
            }
        
        if (keyboard_check_pressed(vk_up))
        or (gamepad_check_pressed_all(gpinput.padu))
            select = max(0,select-1);
        if (keyboard_check_pressed(vk_down))
        or (gamepad_check_pressed_all(gpinput.padd))
            select = min(2,select+1);
        break;
    
    case 5: // keyboard controls
        var _start  = config.keyL1;
        var _end    = config.keySwap1;
        if (rebind)
            {
            var newkey = key_last();
            if (newkey &gt; -1)
                {
                rebind = false;
                
                // resolve conflict by unbinding existing keybind
                for(var i=_start; i&lt;=_end; i++;)
                    {
                    if (global.CFG[i] == newkey)
                        global.CFG[i] = -2;
                    }
                // bind to new key
                if (selecty &gt;= 0) and (selecty &lt;= 6)
                    global.CFG[_start+selecty] = newkey;
                }
            }
        else
            {
            if (pressed)
                {
                if (selecty &gt;= 0) and (selecty &lt;= 6)
                    {
                    rebind = true;
                    // temp unset keybind
                    global.CFG[_start+selecty] = -1;
                    }
                else
                    {
                    // return to settings menu
                    var ready = true;
                    for(var i=_start; i&lt;=_end; i++;)
                        {
                        if (global.CFG[i] &lt; 0)
                            ready = false;
                        }
                    if (ready)
                        {
                        menu = 2;
                        select = 0;
                        }
                    }
                }
            
            if (keyboard_check_pressed(vk_up))
            or (gamepad_check_pressed_all(gpinput.padu))
                selecty = max(0,selecty-1);
            if (keyboard_check_pressed(vk_down))
            or (gamepad_check_pressed_all(gpinput.padd))
                selecty = min(selecty+1,7);
            }
        break;
    
    case 6: // gamepad controls
        if (selectx == 0)
            {
            var _start  = config.padL1;
            var _end    = config.padSwap1;
            }
        else
            {
            var _start  = config.padL2;
            var _end    = config.padSwap2;
            }
        
        if (rebind)
            {
            var newpad = gamepad_last_all();
            if (newpad &gt; -1)
                {
                rebind = false;
                // resolve conflict by unbinding existing keybind
                for(var i=_start; i&lt;=_end; i++;)
                    {
                    if (global.CFG[i] == newpad)
                        global.CFG[i] = -2;
                    }
                // bind to new key
                if (selecty &gt;= 0) and (selecty &lt;= 6)
                    global.CFG[_start+selecty] = newpad;
                }
            }
        else
            {
            if (pressed)
                {
                if (selecty &gt;= 0) and (selecty &lt;= 6)
                    {
                    rebind = true;
                    // temp unset keybind
                    global.CFG[_start+selecty] = -1;
                    }
                else
                    {
                    // return to settings menu
                    if (selectx == 0)
                        {
                        var ready = true;
                        for(var i=config.padL1; i&lt;=config.padSwap2; i++;)
                            {
                            if (global.CFG[i] &lt; 0)
                                ready = false;
                            }
                        if (ready)
                            {
                            menu = 2;
                            select = 0;
                            }
                        }
                    else if (selectx == 1)
                        global.CFG[config.padType] = !global.CFG[config.padType];
                    }
                }
            
            if (keyboard_check_pressed(vk_left))
            or (gamepad_check_pressed_all(gpinput.padl))
                selectx = max(0,selectx-1);
            if (keyboard_check_pressed(vk_right))
            or (gamepad_check_pressed_all(gpinput.padr))
                selectx = min(selectx+1,1);
            if (keyboard_check_pressed(vk_up))
            or (gamepad_check_pressed_all(gpinput.padu))
                selecty = max(0,selecty-1);
            if (keyboard_check_pressed(vk_down))
            or (gamepad_check_pressed_all(gpinput.padd))
                selecty = min(selecty+1,7);
            }
        break;
    }
</string>
          </argument>
        </arguments>
      </action>
    </event>
    <event eventtype="8" enumb="0">
      <action>
        <libid>1</libid>
        <id>603</id>
        <kind>7</kind>
        <userelative>0</userelative>
        <isquestion>0</isquestion>
        <useapplyto>-1</useapplyto>
        <exetype>2</exetype>
        <functionname></functionname>
        <codestring></codestring>
        <whoName>self</whoName>
        <relative>0</relative>
        <isnot>0</isnot>
        <arguments>
          <argument>
            <kind>1</kind>
            <string>var alpha = round(bg_alpha*5)/5;
draw_background_ext(title_BG,0,0,1,1,0,-1,alpha);
draw_background_tiled_ext(title_clouds_BG,CLOUDx,0,1,1,-1,alpha);
draw_background_ext(title_front_BG,0,0,1,1,0,-1,alpha);
draw_background_ext(title_banner_BG,0,0,1,1,0,-1,alpha);
CLOUDx += CLOUDspe;

switch(menu)
    {
    case 0: // title screen
        draw_set_halign(1);
        draw_set_valign(1);
        if (sin(current_time/200) &gt; 0)
            {
            if (sin(current_time/400) &gt; 0)
                draw_text_outline(view_wview[0]/2,230,"Z or X to Start!");
            else
                {
                draw_text_outline(view_wview[0]/2,230," -  or  - to Start!");
                if (global.CFG[config.padType])
                    {
                    draw_sprite(spr_buttons_xbox,0,view_wview[0]/2-64,230);
                    draw_sprite(spr_buttons_xbox,2,view_wview[0]/2-18,230);
                    }
                else
                    {
                    draw_sprite(spr_buttons_ps,0,view_wview[0]/2-64,230);
                    draw_sprite(spr_buttons_ps,2,view_wview[0]/2-18,230);
                    }
                }
            }
                    
        draw_set_halign(0);
        draw_set_valign(0);
        break;
    
    case 1: // main menu
        draw_text_outline(60,120+00,"1 PLAYER START");
        draw_text_outline(60,120+20,"2 PLAYER START");
        draw_text_outline(60,120+40,"OPTIONS");
        draw_text_outline(60,120+60,"QUIT TO DESKTOP");
        
        draw_sprite(spr_camera_arrow,1,32,126+select*20);
        break;
    
    case 2: // settings
        var i = 0;
        draw_text_outline(60,64+i++*18,"VIDEO");
        draw_text_outline(60,64+i++*18,"AUDIO");
        draw_text_outline(60,64+i++*18,"KEYBOARD");
        draw_text_outline(60,64+i++*18,"GAMEPAD");
        draw_text_outline(60,202,"BACK");
        
        if (select &lt;= 3)
            draw_sprite(spr_camera_arrow,1,40,72+select*18);
        else
            draw_sprite(spr_camera_arrow,1,40,210);
        break;
    
    case 3: // video settings
        var i = 0;
        draw_text_outline(60,64+i++*18,"FULLSCREEN: "+val_to_text(0,global.CFG[config.fullscreen]));
        draw_text_outline(60,64+i++*18,"WINDOW SCALE: "+string(global.CFG[config.windowScale])+"X");
        draw_text_outline(60,64+i++*18,"VSYNC: "+val_to_text(0,global.CFG[config.vsync]));
        draw_text_outline(60,64+i++*18,"CRT WARP: "+val_to_text(1,global.CFG[config.CRTwarp]));
        draw_text_outline(60,64+i++*18,"CRT SCANLINES: "+val_to_text(1,global.CFG[config.CRTscan]));
        draw_text_outline(60,64+i++*18,"PARTICLES: "+val_to_text(0,global.CFG[config.particles]));
        draw_text_outline(60,202,"BACK");
        
        if (select &lt; 6)
            draw_sprite(spr_camera_arrow,1,40,72+select*18);
        else
            draw_sprite(spr_camera_arrow,1,40,210);
        break;
    
    case 4: // audio settings
        var i = 0;
        draw_text_outline(60,64+i++*18,"MUSIC VOLUME: "+val_to_text(1,global.CFG[config.BGMgain]));
        draw_text_outline(60,64+i++*18,"SFX VOLUME: "+val_to_text(1,global.CFG[config.SFXgain]));
        draw_text_outline(60,202,"BACK");
        
        if (select &lt; 2)
            draw_sprite(spr_camera_arrow,1,40,72+select*18);
        else
            draw_sprite(spr_camera_arrow,1,40,210);
        break;
    
    case 5: // keyboard controls
        var i = 0;
        draw_text_outline(60,38,"ACTION",$AAAAAA);
        draw_text_outline(60,64+i++*18,"LEFT");
        draw_text_outline(60,64+i++*18,"RIGHT");
        draw_text_outline(60,64+i++*18,"UP");
        draw_text_outline(60,64+i++*18,"DOWN");
        draw_text_outline(60,64+i++*18,"FIRE");
        draw_text_outline(60,64+i++*18,"JUMP");
        draw_text_outline(60,64+i++*18,"LOCK");
        
        var i = 0;
        draw_text_outline(160,38,"KEYBOARD",$AAAAAA);
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyL1])));
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyR1])));
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyU1])));
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyD1])));
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyFire1])))
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyJump1])));
        draw_text_outline(160,64+i++*18,string(key_to_text(global.CFG[config.keyLock1])));
        
        draw_text_outline(60,202,"BACK");
        
        if (selecty &lt; 7)
            draw_sprite(spr_camera_arrow,1-rebind,140,72+selecty*18);
        else
            draw_sprite(spr_camera_arrow,1,40,210);
        break;
    
    case 6: // gamepad controls
        var i = 0;
        draw_text_outline(60,38,"ACTION",$AAAAAA);
        draw_text_outline(60,64+i++*18,"LEFT");
        draw_text_outline(60,64+i++*18,"RIGHT");
        draw_text_outline(60,64+i++*18,"UP");
        draw_text_outline(60,64+i++*18,"DOWN");
        draw_text_outline(60,64+i++*18,"FIRE");
        draw_text_outline(60,64+i++*18,"JUMP");
        draw_text_outline(60,64+i++*18,"LOCK");
        
        if (global.CFG[config.padType])
            var pad_spr = spr_buttons_xbox;
        else
            var pad_spr = spr_buttons_ps;
        
        var i = 0;
        draw_text_outline(160,38,"GAMEPAD 1",$AAAAAA);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padL1]),160,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padR1]),160,72+i++*18)
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padU1]),160,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padD1]),160,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padFire1]),160,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padJump1]),160,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padLock1]),160,72+i++*18);
        
        var i = 0;
        draw_text_outline(284,38,"GAMEPAD 2",$AAAAAA);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padL2]),284,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padR2]),284,72+i++*18)
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padU2]),284,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padD2]),284,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padFire2]),284,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padJump2]),284,72+i++*18);
        draw_sprite(pad_spr,pad_to_sprite(global.CFG[config.padLock2]),284,72+i++*18);
        
        draw_text_outline(60,202,"BACK");
        draw_text_outline(170,202,"TOGGLE STYLE");
        
        if (selecty &lt; 7)
            draw_sprite(spr_camera_arrow,1-rebind,140+selectx*124,72+selecty*18);
        else
            draw_sprite(spr_camera_arrow,1,40+selectx*110,210);
        break;
    }

draw_set_color($000000);

if (menu &gt; 0)
    bg_alpha = max(0.25,bg_alpha-0.05);
else
    bg_alpha = min(1.00,bg_alpha+0.05);
</string>
          </argument>
        </arguments>
      </action>
    </event>
  </events>
  <PhysicsObject>0</PhysicsObject>
  <PhysicsObjectSensor>0</PhysicsObjectSensor>
  <PhysicsObjectShape>0</PhysicsObjectShape>
  <PhysicsObjectDensity>0.5</PhysicsObjectDensity>
  <PhysicsObjectRestitution>0.100000001490116</PhysicsObjectRestitution>
  <PhysicsObjectGroup>0</PhysicsObjectGroup>
  <PhysicsObjectLinearDamping>0.100000001490116</PhysicsObjectLinearDamping>
  <PhysicsObjectAngularDamping>0.100000001490116</PhysicsObjectAngularDamping>
  <PhysicsObjectFriction>0.200000002980232</PhysicsObjectFriction>
  <PhysicsObjectAwake>-1</PhysicsObjectAwake>
  <PhysicsObjectKinematic>0</PhysicsObjectKinematic>
  <PhysicsShapePoints/>
</object>
